function eks_switch --argument-names profile --description "Connect to an EKS cluster in the given AWS profile"
    function __handle_aws_credentials_error --description "Helper function to catch AWS credentials errors"
        echo $RED"Check your credentials"$NORMAL
        set --erase AWS_PROFILE
        functions --erase __handle_aws_credentials_error
    end

    set --global --export AWS_PROFILE $profile
    kubectl config unset current-context >/dev/null

    echo $GREEN"----------------| AWS Identity |----------------
    "$NORMAL
    aws --profile $profile sts get-caller-identity --no-cli-pager

    switch $status
    case 0
        # https://docs.aws.amazon.com/cli/latest/userguide/cli-usage-returncodes.html
        # 0: The service responded with an HTTP response status code of 200 indicating that there were no errors generated by the AWS CLI and AWS service the request was sent to.
    case '*'
        type --query creds; or __handle_aws_credentials_error && return
        creds && sleep 5 && aws --profile $profile sts get-caller-identity --no-cli-pager; or __handle_aws_credentials_error && return
    end
    echo

    set --function cluster_name $(aws --profile $profile eks list-clusters --query clusters | jq --raw-output .[] | fzf --prompt="Select a cluster: ")

    if test -n "$cluster_name"
        aws --profile $profile eks update-kubeconfig --name $cluster_name --region eu-central-1
        echo && echo $GREEN"Connected to the $cluster_name cluster with the $profile AWS profile."$NORMAL
    else
        echo $RED"No cluster was selected, so the Kubernetes context was not updated."$NORMAL
    end
end
